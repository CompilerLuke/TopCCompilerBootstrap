import "types"
from "error" import Thrower

type OperatorKind either
    Add or Mul or Div or LT or Sub

type ReadInfo =
    is_global: bool
    name: string

type FuncInfo =
    name: string
    _type: &types.FuncPtr

type Payload either
    Root
    Int(string)
    Float(string)
    Bool(bool)
    Operator(OperatorKind)
    Identifier(&ReadInfo)
    FuncCall
    CreateAssign or Create or Assign
    If or Block or Condition or Else
    While
    FuncDef(&FuncInfo)
    Tuple
    FuncArg(string, types.CompilerType)
    String(string)

def Payload.toString(&self) string =
    payload_type := get_type Payload
    payload_type.cases[self.tag].name

type AST =
    payload: Payload
    nodes: [..]&AST
    _type: types.CompilerType
    owner: Maybe[&AST]

    _filename: string
    _line: uint
    _column: uint

def AST.get_filename(&self) string = self._filename
def AST.get_line(&self) uint = self._line
def AST.get_column(&self) uint = self._column

def AST.add(&self, node: &AST) =
    node.owner = Some self
    self.nodes.append node

def AST.op_get(&self, index: uint) &&AST =
    &self.nodes[index]

def AST.length(&self) uint =
    self.nodes.length

def make_AST[T: Thrower](t: T, payload: Payload) &AST =
    nodes := [..]&AST
    nodes.allocator = Some context.longterm_storage

    box_longterm AST{
        payload = payload
        nodes = nodes
        owner = None
        _type = types.Void
        _filename = t.get_filename!
        _line = t.get_line!
        _column = t.get_column!
    }

def op_type(node: &AST) types.CompilerType =
    assert node.payload.tag == (Operator Add).tag, "Passing incorrect node to op_type"
    node.nodes[0]._type